plugins {
    id 'java'
    id 'jacoco'
    id 'groovy'
    id 'application'
    id 'org.springframework.boot' version '3.4.3'
}

java {
    sourceCompatibility = '21'
}

application {
    mainClass = 'ru.homevault.authserver.Application'
}

task runApp(type: JavaExec, dependsOn: build) {
    group = 'application'
    description = 'Runs the Home Vault Auth Server application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'ru.homevault.authserver.Application'
    jvmArgs = ["-Dspring.profiles.active=$profile"]
}

tasks.register("deployJar") {
    group = "build"
    dependsOn("test", "bootJar")
    tasks.findByName("bootJar")?.mustRunAfter("test")
    doLast {
        def deployDir = file("$project.rootDir/auth-server/deploy")
        def jarFiles = fileTree("$buildDir/libs").matching { include '*.jar' }.files

        if (jarFiles.isEmpty()) {
            throw new GradleException("JAR file not found in ${buildDir}/libs!")
        }

        delete deployDir
        mkdir deployDir

        copy {
            from jarFiles
            into deployDir
            rename { 'app.jar' }
        }
    }
}

ext {
    springBootVersion = '3.4.3'
    lombokVersion = '1.18.36'
    spockFrameworkRelease = '4.0'
    spockFrameworkVersion = '2.4'
    mapStructVersion = '1.6.2'
    auth0JwtVersion = '4.5.0'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

    implementation "org.mapstruct:mapstruct:$mapStructVersion"

    implementation "com.auth0:java-jwt:$auth0JwtVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"

    annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.spockframework:spock-core:$spockFrameworkVersion-M5-groovy-$spockFrameworkRelease"
    testImplementation "org.spockframework:spock-spring:$spockFrameworkVersion-M5-groovy-$spockFrameworkRelease"
    testImplementation "org.spockframework:spock-junit4:$spockFrameworkVersion-M5-groovy-$spockFrameworkRelease"

    testImplementation "org.springframework.boot:spring-boot-test-autoconfigure:$springBootVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}

compileJava {
    options.compilerArgs += ['-Amapstruct.defaultComponentModel=spring']
}

test {
    useJUnitPlatform()
}
